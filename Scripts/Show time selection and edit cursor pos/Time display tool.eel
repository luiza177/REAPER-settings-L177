// Time display tool (EEL script by spk77 21.7.2014)
// Use mouse wheel to increase/decrease font size
// left click on time string -> cycle through time formatting modes

font_size = 16;
font_name = "Verdana";

function adjust_font_size(mouse_wheel_val)
(
  mouse_wheel_val > 0 ? (
    font_size < 20 ? (
      font_size += 2;
    );
  );
  
  mouse_wheel_val < 0 ? (
    (font_size > 12 ) ? (
      font_size -= 2;
    );
  );
  gfx_setfont(1, font_name, font_size);
  mouse_wheel = 0;
);

function init(window_w, window_h)
(
  gfx_a = 1; gfx_r = 1; gfx_g = 1; gfx_b = 1;
  gfx_init("Time display tool", window_w, window_h);
  gfx_setfont(1, font_name, font_size);
);

function run() local(time_range, time_sel_start, time_sel_end)
(
  gfx_a = 0.5; gfx_r = 1; gfx_g = 1; gfx_b = 1;
  gfx_x = 10;
  gfx_y = 10;
  GetSet_LoopTimeRange(0, 0, time_sel_start, time_sel_end, 0);
  time_range = time_sel_end - time_sel_start;
  format_timestr_pos(time_range, #ts_buf, ts_format);
  format_timestr_pos(GetCursorPosition(), #ec_buf, ec_format);
  
  
  y_ts = gfx_y;
  gfx_printf("Time selection:  ");
  x_ts = gfx_x;
  
  gfx_x = 10;
  gfx_y += 2 * gfx_texth;
  
  y_ec = gfx_y;
  gfx_printf("Edit cursor pos: ");
  x_ec = gfx_x;
  
  //gfx_printf(#buf);
  gfx_measurestr(#ts_buf, w_ts, 0);
  gfx_measurestr(#ec_buf, w_ec, 0);
  
  mouse_wheel != 0 ? adjust_font_size(mouse_wheel);
  
  gfx_a = 1; gfx_r = 1; gfx_g = 1; gfx_b = 1;
  
  mouse_x > x_ts && mouse_x < x_ts + w_ts && mouse_y > y_ts && mouse_y < y_ts + gfx_texth /*&& mouse_state == 0*/ ? (
    gfx_x = 10;
    gfx_y = 4 * gfx_texth;
    
    ts_format == 0 ? (
      gfx_printf("Current time formatting mode:\n(h:)m:s.ms");
    ) : ts_format == 1 ? (
      gfx_printf("Current time formatting mode:\nmeasures.beats.time");
    ) : ts_format == 2 ? (
      gfx_printf("Current time formatting mode:\nmeasures.beats");
    ) : ts_format == 3 ? (
      gfx_printf("Current time formatting mode:\nseconds");
    ) : ts_format == 4 ? (
      gfx_printf("Current time formatting mode:\nsamples");
    ) : ts_format == 5 ? (
      gfx_printf("Current time formatting mode:\nh:m:s:frames");
    );

    gfx_r = 0.5; gfx_g = 0.5;
    mouse_cap == 1 ? (
      //gfx_r = 0.5; gfx_g = 0.5;
      mouse_state == 0 ? (
        mouse_state = 1;
        ts_format < 5 ? ts_format += 1 : ts_format = 0;
      );
    );
  );
  gfx_x = x_ts;
  gfx_y = y_ts;
  gfx_printf(#ts_buf);
  gfx_a = 1; gfx_r = 1; gfx_g = 1; gfx_b = 1;
  
  mouse_x > x_ec && mouse_x < x_ec + w_ec && mouse_y > y_ec && mouse_y < y_ec + gfx_texth /*&& mouse_state == 0*/ ? (
    gfx_x = 10;
    gfx_y = 4 * gfx_texth;
    
    ec_format == 0 ? (
      gfx_printf("Current time formatting mode:\n(h:)m:s.ms");
    ) : ec_format == 1 ? (
      gfx_printf("Current time formatting mode:\nmeasures.beats.time");
    ) : ec_format == 2 ? (
      gfx_printf("Current time formatting mode:\nmeasures.beats");
    ) : ec_format == 3 ? (
      gfx_printf("Current time formatting mode:\nseconds");
    ) : ec_format == 4 ? (
      gfx_printf("Current time formatting mode:\nsamples");
    ) : ec_format == 5 ? (
      gfx_printf("Current time formatting mode:\nh:m:s:frames");
    );
    
    gfx_r = 0.5; gfx_g = 0.5;
    mouse_cap == 1 ? (
      //gfx_r = 0.5; gfx_g = 0.5;
      mouse_state == 0 ? (
        mouse_state = 1;
        ec_format < 5 ? ec_format += 1 : ec_format = 0;
      );
    );
  );
  gfx_x = x_ec;
  gfx_y = y_ec;
  gfx_printf(#ec_buf);
  gfx_a = 1; gfx_r = 1; gfx_g = 1; gfx_b = 1;

  mouse_cap == 0 ? mouse_state = 0;// : mouse_state = 1;
  gfx_update();
  gfx_getchar() >= 0 ? defer("run();");
);

// init(window_w, window_h);
init(220, 120);
run();